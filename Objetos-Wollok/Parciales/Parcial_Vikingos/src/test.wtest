
import vikingo.*
import expedicion.*
import aldeayCapital.*


describe "Tests Vikingos" {
		var aldea = new Aldea(cantidadDeCrucifijos = 20) 
		var aldeaAmurallada = new AldeaAmurallada(cantidadDeCrucifijos = 20, cantidadMinimaVikingos = 10) 
		var capital = new Capital(cantidadDefensores = 4, factorDeRiqueza = 20) 
		var soldadoArmado = new Soldado(vidasCobradas = 30,cantidadArmas = 10)
		var soldadoDesarmado = new Soldado(vidasCobradas = 30, cantidadArmas = 0)
		var granjeroProductivo = new Granjero(cantidadDeHijos = 2, cantidadDeHectareasDesignadas = 4)
		var granjeroImproductivo = new Granjero(cantidadDeHijos = 2, cantidadDeHectareasDesignadas = 0)
		var expedicion = new Expedicion(puntosDeInteres = [aldea, capital])
	

	method armarExpedicion(){
		soldadoArmado.casta(new Karl(vikingo=soldadoArmado))
		expedicion.subir(granjeroProductivo)
		expedicion.subir(soldadoArmado)
	}
	
	// tests subir
    test "Puedo agregar soldado karl armado a la expedicion "{
    	soldadoArmado.casta(new Karl(vikingo=soldadoArmado))
    	expedicion.subir(soldadoArmado)
    	assert.that(expedicion.vikingosQueLaIntegran().contains(soldadoArmado))
    }
     test "No puedo agregar soldado jarl con armas a la expedicion "{
    	assert.throwsExceptionLike(noPuedeSubirExpedicion, { expedicion.subir(soldadoArmado)})
    }
     test "No puedo agregar a soldado karl sin armas a la expedicion "{
    	soldadoDesarmado.casta(new Karl(vikingo=soldadoArmado))
    	assert.throwsExceptionLike(noPuedeSubirExpedicion, { expedicion.subir(soldadoDesarmado)})
    }
    test "Puedo agregar granjero productivo a la expedicion "{
    	expedicion.subir(granjeroProductivo)
    	assert.that(expedicion.vikingosQueLaIntegran().contains(granjeroProductivo))
    }
     test "No puedo agregar granjero improductivo a la expedicion "{
    	assert.throwsExceptionLike(noPuedeSubirExpedicion, { expedicion.subir(granjeroImproductivo)})
    }
    // Tests Vale la pena
	test "Aldea vale la pena para 2 invasores"{
		assert.that(aldea.valeLaPena(2))
	}
	test "Capital vale la pena para 2 invasores"{
    	self.armarExpedicion() 
		assert.that(capital.valeLaPena(2))
	}
	test "Aldea Amurallada no vale la pena para 2 invasores"{
    	self.armarExpedicion() 
		assert.notThat(aldeaAmurallada.valeLaPena(2))
	}
	
    test "expedicion con aldea amurallada no vale la expedicion"{
    	self.armarExpedicion() 
    	expedicion.puntosDeInteres().add(aldeaAmurallada)
    	assert.notThat(expedicion.valeLaPena())
    }
    test "expedicion vale la pena"{
    	self.armarExpedicion() 
    	assert.that(expedicion.valeLaPena())
    }
	// tests realizar expedición
	
	test "Realizar expedición disminuye crucifijos"{
    	self.armarExpedicion()
    	expedicion.realizar() 
    	assert.equals(0,aldea.cantidadDeCrucifijos())
    }
    test "Realizar expedición disminuye defensores"{
    	self.armarExpedicion()
    	expedicion.realizar() 
    	assert.equals(2,capital.cantidadDefensores())
    }
    test "Realizar expedición aumenta vidas cobradas de soldado"{
    	self.armarExpedicion() 
    	expedicion.realizar()
    	assert.equals(31,soldadoArmado.vidasCobradas())
    }
    test "Capital con pocos defensores al ser invadida no recompensa con vidas a todos los invasores"{
    	self.armarExpedicion()
    	capital.cantidadDefensores(1)
    	expedicion.realizar() 
    	assert.equals(30,soldadoArmado.vidasCobradas())
    }
    test "Realizar expedición aumenta correctamente dinero de integrantes"{
    	self.armarExpedicion()
    	expedicion.realizar() 
    	assert.equals((2*20+20)/2,soldadoArmado.botinAhorrado())
    	assert.equals((2*20+20)/2,granjeroProductivo.botinAhorrado())
    }
}
